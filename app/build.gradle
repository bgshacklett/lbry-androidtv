plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    alias(libs.plugins.protobuf)
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            keyAlias keystoreProperties['keyAlias']
        }
    }
    compileSdk 31
    defaultConfig {
        applicationId "app.newproj.lbrytv"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0.0"
        ndk.abiFilters 'armeabi-v7a'
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlin {
            kotlinOptions {
                freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
            }
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

protobuf {
    protoc {
        artifact = libs.protoc.get()
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation libs.lbry32
    implementation libs.bundles.exoplayer
    implementation libs.bundles.leanback
    implementation libs.bundles.lifecycle
    implementation libs.bundles.navigation
    implementation libs.bundles.room
    implementation libs.bundles.test
    implementation libs.core
    implementation libs.datastore
    implementation libs.fragment.ktx
    implementation libs.glide
    implementation libs.gson
    implementation libs.hilt.android
    implementation libs.okhttp.logging.interceptor
    implementation libs.protobuf.javalite
    implementation libs.retrofit
    implementation libs.retrofit.gson
    implementation libs.splashscreen
    implementation libs.startup
    implementation libs.timber
    kapt libs.glide.compiler
    kapt libs.hilt.compiler
    kapt libs.room.compiler
}
